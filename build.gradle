group 'LGP'
version '3.0'

buildscript {
    ext.kotlin_version = '1.3.11'
    ext.dokka_version = '0.9.17'
    ext.kotlinx_coroutines_version = '1.0.1'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    // https://mvnrepository.com/artifact/com.opencsv/opencsv
    compile group: 'com.opencsv', name: 'opencsv', version: '4.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'

    // For YAML parsing
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.2'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"      \
                         << "-Xlint:all"            \
                         << "-Xlint:-processing"    \
                         << "-Xlint:-serial"        \
                         << "-Werror"
}

task dokkaApiHtml(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/../docs/api/html"
}


task dokkaApiJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/../docs/api/javadoc"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task examplesJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'LGP',
                'Implementation-Version': version
    }

    include('lgp/examples/*')
    include('lgp/examples/java/*')
    exclude('lgp/core')
    exclude('lgp/lib')

    baseName = project.name + '-examples'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task coreJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'LGP',
                'Implementation-Version': version
    }

    exclude('lgp/examples')

    baseName = project.name + '-core'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task printVersion {
    doLast {
        println project.version
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = System.getenv('GPG_KEY_ID') }
        allprojects { ext."signing.secretKeyRingFile" = System.getenv('GPG_KEY_LOCATION') }
        allprojects { ext."signing.password" = System.getenv('GPG_PASSPHRASE') }
    }
    // Do not sign archives by default (a local build without gpg keyring should succeed)
    if (taskGraph.allTasks.any { it.name == 'build' || it.name == 'assemble' }) {
        tasks.findAll { it.name == 'signArchives' || it.name == 'signDocsJar' || it.name == 'signTestJar' }.each { task ->
            task.enabled = false
        }
    }
}
signing {
    sign configurations.archives
}

build.dependsOn.remove(signArchives)

artifacts {
    archives sourcesJar, javadocJar
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
            }

            // Add required metadata to POM
            pom.project {
                name 'LGP'
                packaging 'jar'
                description 'A robust Linear Genetic Programming implementation on the JVM using Kotlin. '
                url 'https://github.com/JedS6391/LGP'

                scm {
                    connection 'scm:git:git://github.com/JedS6391/LGP.git'
                    developerConnection 'scm:git:ssh://github.com/JedS6391/LGP.git'
                    url 'http://github.com/JedS6391/LGP/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/JedS6391/LGP/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'jedsimson'
                        name 'Jed Simson'
                        email 'jed.simson@gmail.com'
                        organization 'jedsimson'
                        organizationUrl 'https://jedsimson.co.nz'
                    }
                }
            }
        }
    }
}
